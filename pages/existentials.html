<title>posts :: kangaroo courts & existential packing</title>
<main>
<h1>(* Kangaroo Courts & Existential Packing *)</h1>

<p>
  As a reasonable adult, you probably don't believe in elves. What's a surefire way of convincing you that elves are real? If I walked up to
  you &mdash; squiggly creatures in hand and no deceitful contraption in sight &mdash; would
  that be enough?
</p>

<span class="aside">This post is ideally an accessible rehash of &sect;2.1 of Will Crichton's paper "Typed Design Patterns for the Functional Era". I urge you to read that paper for more typed patterns :)</span>

<p>
  Assume for a moment that elves are real and they are magical creatures
  that always tell the truth. Philosophical conundrums notwithstanding, it means
  that if you were unlucky enough to be spotted by an elf while you were stealing
  cookies you are going straight to jail.
</p>

<p>Let's code up such a system! In this hypothetical world, there are only a few kinds of testimonies that anyone can establish: they saw or heard you in the act; they can vouch for your character or they have you on camera.
</p>

<pre><code class="language-ocaml">
type 'a evidence =
  | Eyewitness
  | Character of bool
  | Video of Bytes.t
</code></pre>

<p>Now, all testimonies are not created equal. We know that the word of an elf is infallible.</p>

<pre><code class="language-ocaml">
type 'a testimony =
  | Human of 'a evidence
  | Elf of 'a evidence
</code></pre>

<p>
  Armed with these two types and a deus ex machina that determines whether the video evidence is legit or not, we can construct a function to determine if you are absolutely guilty.
</p>

<pre><code class="language-ocaml">
let guilty : 'a testimony -> bool =
  fun w -> match w with
  | Human (Character _) | Elf (Character _) -> false
  | Human Eyewitness -> false
  | Elf Eyewitness -> true
  | Human (Video b) | Elf (Video b) -> discern_video b
</code></pre>

<span class="aside">Or else, you might relive my childhood nightmare "I'm not Martin" by R.L Stine.</span>

<p>
  The process of jailing someone for sugary crimes is a messy business though. You might have dozens of functions that go through the necessary bureaucracy and you need to make sure at every step of the way that you have the right person. For example, you might want to remove the toys from their rooms ...
</p>

<pre><code class="language-ocaml">
let remove_toys (p : perpetrator) = remove (find_room p)
</code></pre>

<p>
  But this function does not check if our perpetrator is actually guilty. You can imagine a scheming sibling backdooring the judicial process by calling this function willy-nilly. Or more realistically, you make a mistake while you're coding up your system and forget to check some invariant. Maybe it's expensive to verify the invariants constantly, yet you still want to statically ensure that you do not call the wrong code at the wrong time.
</p>

<p>
  In which case, let's reframe these functions and pretend that they are cynical &mdash; they don't believe that the person is guilty, but you can convince them by presenting undeniable proof. So, let's modify our functions ...
</p>

<pre><code class="language-ocaml">
type proof = Exists

let guilty : 'a testimony -> proof option =
  fun w ->
  match w with
  | Human (Character _) | Elf (Character _) -> None
  | Human Eyewitness -> None
  | Elf Eyewitness -> Some Exists
  | Human (Video b) | Elf (Video b) -> discern_video b

let remove_toys (_proof : proof) (p : perpetrator) = remove (find_room p)
</code></pre>

<p>
  Our incarceration apparatus now requires a <code>proof</code> value to be supplied and the only way to get a <code>proof</code> is to call our guilty function.
</p>

<pre><code class="language-ocaml">
let w = Elf Eyewitness

(* Ideally ... *)
let () = match (guilty w) with
  | Some p -> remove_toys p "Jamie"
  | None -> release ()
</code></pre>

<p>Except we can always just use an escape hatch and call the function directly, haha!</p>

<pre><code class="language-ocaml">
let () = remove_toys Exists "Jamie"
</code></pre>

<p>
  The problem here is that we have more information than we are supposed to. We know that our functions are gatekept by this <code>proof</code> type and we know how to construct one directly. The solution is to keep this information away, or keep it abstract as they say, by wrapping it in a module and specifying its signature.
</p>

<pre><code class="language-ocaml">
module Proof : sig
  type t

  val guilty : 'a testimony -> t option
end = struct
  type t = Exists

  let guilty : 'a testimony -> t option =
    fun w ->
    match w with
    | Human (Character _) | Elf (Character _) -> None
    | Human Eyewitness -> None
    | Elf Eyewitness -> Some Exists
    | Human (Video b) | Elf (Video b) -> discern_video b
end
</code></pre>

<p>
  When you use this module, all you know is that there exists some type <code>t</code>
  and that there is a function that constructs <code>t options</code> from testimonies called
  <code>guilty</code>. When you write this module you associate this "existence"
  with a concrete value, but as a user you have no more information. Now, functions
  that accept this <code>Proof.t</code> don't actually care about the internals
  of the value but the fact that you can produce one &mdash; in a similar vein
  to how you did not care about how I brought an elf to our meeting, only that
  they are in the flesh.
</p>

<p>
  But the fact that you cannot peer into the module and see how it works is precisely
  that which prevents you from creating fake testimonies and bypassing the safeguards
  that you put in place to begin with. Most importantly, you can rest assured
  within the body of the function that the suspect is guilty and go ham!
</p>

<h3>(*** References ***)</h3>

<ul>
  <li>Crichton, Will. “Typed Design Patterns for the Functional Era.” Proceedings of the 1st ACM SIGPLAN International Workshop on Functional Software Architecture, August 30, 2023, 40-48. <a href="https://doi.org/10.1145/3609025.3609477">DOI</a>
  </li>
  <li>
    Pierce, Benjamin C. "Existential Types" Types and Programming Languages, 363 - 77. Cambridge: The MIT Press, 2019.
  </li>
</ul>

<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/highlight.min.js"></script>
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.9.0/languages/ocaml.min.js"></script>
<script>hljs.highlightAll();</script>